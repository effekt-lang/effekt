import immutable/list
import immutable/option
import io/args
import text/string

effect Get() : Int
effect Set(i: Int): Unit

def countdown(): Int / {Get, Set} = {
  val i = do Get();
  if (i == 0) {
    i
  } else {
    do Set(i - 1);
    countdown()
  }
}

def run(n: Int) = {
  var s = n;
  try {
    countdown()
  } with Get { () => resume(s)
  } with Set { (i: Int) => s = i; resume(()) }
}

def main() = {
  val n = commandLineArgs().headOption.getOrElse { "" }.toInt.getOrElse { 5 };
  val r = run(n);
  println(r)
}