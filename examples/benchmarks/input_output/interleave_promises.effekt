import examples/benchmarks/runner

import bytearray
import list
import io
import io/error
import io/filesystem


def run(n: Int) = {
  with on[IOError].panic;

  val foldername = "/tmp/interleave_promises/"
  val size = 4096

  on[IOError].result { mkdir(foldername) } match {
    case Error(EEXIST(), msg) => ()
    case Error(e, m) => do raise(e, m)
    case Success(()) => ()
  }

  val promises = list::build(n) { i =>
    promise(box {
      with on[IOError].result
      val filename = foldername ++ i.show ++ ".txt"
      val file = open(filename, WriteOnly())
      val buffer = bytearray(size, 35.toByte)
      repeat(8) {
        val i = write(file, buffer, 0, size, -1)
        if (i < size) {
          panic("failed write")
        }
      }
      close(file)
    })
  }

  promises.foreachIndex { (i, p) =>
    p.await().value()
    val filename = foldername ++ i.show ++ ".txt"
    val file = open(filename, ReadOnly())
    val buffer = bytearray::allocate(size)
    repeat(8) {
      val i = read(file, buffer, 0, size, -1)
      if (i < size) {
        panic("failed read")
      }
    }
    close(file)
  }

  return 0
}

def main() = benchmark(5){run}

