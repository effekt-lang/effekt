import examples/benchmarks/runner

import io
import io/error
import io/network
import bytearray

def run(n: Int) = {
  with on[IOError].panic

  val listener = bind("127.0.0.1", 8080);
  spawn(box {
    with on[IOError].panic
    listen(listener, box { connection =>
      with on[IOError].panic
      val message = "hello world"
      var buffer = bytearray::fromString(message)
      write(connection, buffer, 0, buffer.size())
      close(connection)
  })});

  val results = array::build(n) { i =>
    promise(box {
      with on[IOError].result
      val connection = connect("127.0.0.1", 8080)
      var buffer = bytearray::allocate(4096)
      val number = read(connection, buffer, 0, 4096)
      close(connection)
      number
    })
  };

  var total = 0;
  results.foreach { number =>
    total = total + number.await.value
  };

  shutdown(listener)
  
  return total
}

def main() = benchmark(5){run}
