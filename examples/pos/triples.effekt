module triples

import list

effect flip(): Bool
effect fail(): Nothing

type Triple {
  MkTriple(x: Int, y: Int, z: Int)
}

def show(t: Triple): String = t match {
  case MkTriple(x, y, z) => "MkTriple(" ++ show(x) ++ ", " ++ show(y) ++ ", " ++ show(z) ++ ")"
}

def choice(n : Int): Int / { flip, fail } =
  if (n < 1) {
    do fail()
  } else if (do flip()) {
    n
  } else {
    choice(n - 1)
  }

def triple(n: Int, s: Int) = {
  val i = choice(n);
  val j = choice(i - 1);
  val k = choice(j - 1);
  if ((i + j + k) == s) {
    MkTriple(i, j ,k)
  } else {
    do fail()
  }
}

def handledTriple(n : Int, s : Int) =
  try {
    // lift inference infers two lifts here, which is not necessary!
    // but how can we tell functions like `triple` apart from capabilities?
    // what if a local function closes over some capabilities and receives a few others?
    // what if that function is used under a handler?
    try { Cons(triple(n, s), Nil[Triple]()) }
    with fail { () => Nil[Triple]() }
  } with flip { () => append(resume(true), resume(false)) }

def main() = println(show(handledTriple(10, 15)))
