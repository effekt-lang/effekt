module effekt

namespace js {
  // Runtime
  extern include js "../js/effekt_runtime.js"

  // Builtins
  extern include js "../js/effekt_builtins.js"
}

namespace chez {

  // All Chez Scheme backends
  // ------------------------
  extern include chez "../chez/common/effekt_primitives.ss"

  // call/cc based implementation
  // ----------------------------
  extern include chezCallCC "../chez/callcc/seq0.ss"
  extern include chezCallCC "../chez/callcc/tail0.ss"
  extern include chezCallCC "../chez/callcc/effekt.ss"

  // CPS implementation
  // ------------------
  extern include chezCPS "../chez/cps/effekt.ss"

  // Monadic implementation
  // ----------------------
  extern include chezMonadic "../chez/monadic/seq0.ss"
  extern include chezMonadic "../chez/monadic/effekt.ss"
}

namespace llvm {
  extern include llvm "../llvm/rts.ll"
  extern include llvm "../llvm/forward-declare-c.ll"
}

// Showing and debugging
// =====================

extern def println(value: String): Unit =
  js "$effekt.println(${value})"
  chez "(println_impl ${value})"
  llvm """
    call void @c_io_println(%Pos ${value})
    ret %Pos zeroinitializer ; Unit
  """
  vm "effekt::println(String)"

extern jsNode """
$effekt.readln = function readln$impl(callback) {
  const readline = require('node:readline').createInterface({
    input: process.stdin,
    output: process.stdout,
  });
  readline.question('', (answer) => {
    readline.close();
    callback(answer);
  });
}
"""

extern def readln() at async: String =
  jsNode "$effekt.capture(callback => $effekt.readln(callback))"
  llvm """
    %result = call %Pos @c_io_readln()
    tail call void @resume_Pos(%Stack %stack, %Pos %result)
    ret void
  """

def println(value: Int): Unit = println(value.show)
def println(value: Unit): Unit = println(value.show)
def println(value: Double): Unit = println(value.show)
def println(value: Bool): Unit = println(value.show)
def println(value: Byte): Unit = println(value.show)

extern def show(value: Int) at {}: String =
  js "'' + ${value}"
  chez "(show-number ${value})"
  llvm """
    %z = call %Pos @c_bytearray_show_Int(%Int ${value})
    ret %Pos %z
  """
  vm "effekt::show(Int)"

def show(value: Unit): String = "()"

extern def show(value: Double) at {}: String =
  js "'' + ${value}"
  chez "(show-number ${value})"
  llvm """
    %z = call %Pos @c_bytearray_show_Double(%Double ${value})
    ret %Pos %z
  """
  vm "effekt::show(Double)"

def show(value: String): String =
  value

def show(value: Bool): String =
  if (value) "true" else "false"

extern def show(value: Char) at {}: String =
  js "String.fromCodePoint(${value})"
  chez "(string (integer->char ${value}))"
  llvm """
    %z = call %Pos @c_bytearray_show_Char(%Int ${value})
    ret %Pos %z
  """
  vm "effekt::show(Char)"

extern def show(value: Byte) at {}: String =
  js "'' + ${value}"
  chez "(string ${value})"
  llvm """
    %z = call %Pos @c_bytearray_show_Byte(i8 ${value})
    ret %Pos %z
  """
  vm "effekt::show(Byte)"

extern def genericShow[R](value: R) at {}: String =
  js "$effekt.show(${value})"
  chez "(show_impl ${value})"

extern def inspect[R](value: R) at io: Unit =
  js { println(genericShow(value)) }
  chez { println(genericShow(value)) }
  vm "effekt::inspect(Any)"


// Strings
// =======
extern def infixConcat(s1: String, s2: String) at {}: String =
  js "${s1} + ${s2}"
  chez "(string-append ${s1} ${s2})"
  llvm """
    %spz = call %Pos @c_bytearray_concatenate(%Pos ${s1}, %Pos ${s2})
    ret %Pos %spz
  """
  vm "effekt::infixConcat(String, String)"

extern def length(str: String) at {}: Int =
  js "${str}.length"
  chez "(string-length ${str})"
  llvm """
    %x = call %Int @c_bytearray_size(%Pos ${str})
    ret %Int %x
  """
  vm "effekt::length(String)"

extern def unsafeSubstring(str: String, from: Int, to: Int) at {}: String =
  js "${str}.substring(${from}, ${to})"
  chez "(substring ${str} ${from} ${to})" // potentially raises: "Exception in substring: ..."
  llvm """
    %x = call %Pos @c_bytearray_substring(%Pos ${str}, i64 ${from}, i64 ${to})
    ret %Pos %x
  """
  vm "effekt::substring(String, Int, Int)"


// Side effecting ops
// ==================

extern def random() at io: Double =
  js "Math.random()"
  chez "(random 1.0)"
  llvm """
    %r = call double @c_io_random()
    ret double %r
  """
  vm "effekt::random()"

// References and state
// ====================
// the following type is defined in builtins.scala
// interface Ref[T] { def get(): T; def put(t: T): Unit }
// extern io def freshIn[T](t: T) {r: Region}: Ref[T] at r = js "r.fresh(${t})"
// extern io def freshGlobal[T](t: T): Ref[T] at global = js "$effekt.fresh(${t})"


// Equality
// ========

/// Result of comparison between two objects according to some order:
///
/// - `Less()` means that the first object is *before* the first in the order,
/// - `Equal()` means that the two objects are the same in the order,
/// - and `Greater()` means that the second object is *before* the first in the order.
type Ordering {
  Less();
  Equal();
  Greater()
}

def compareInt(n: Int, m: Int) =
  if (n == m) Equal()
  else if (n < m) Less()
  else Greater()

extern def genericCompareImpl[R](x: R, y: R) at {}: Int =
  js "$effekt.compare(${x}, ${y})"

/// Compares two values of the same type, returning an `Ordering`.
/// Only available on the JavaScript backend.
def genericCompare[R](x: R, y: R): Ordering = {
  val ret = genericCompareImpl(x, y)
  if (ret == 0) {
    Equal()
  } else if (ret < 0) {
    Less()
  } else { // ret > 0
    Greater()
  }
}

def show(o: Ordering): String = o match {
  case Less() => "Less()"
  case Equal() => "Equal()"
  case Greater() => "Greater()"
}

def println(o: Ordering): Unit = println(o.show)


// Comparison ops
// ==============

/// Structural equality: Not available in the LLVM backend
extern def equals[R](x: R, y: R) at {}: Bool =
  js "$effekt.equals(${x}, ${y})"
  chez "(equal_impl ${x} ${y})"

def differsFrom[R](x: R, y: R): Bool =
  not(equals(x, y))

// TODO fix this in the pattern matching compiler
extern def infixEq(x: Unit, y: Unit) at {}: Bool =
  default { true }

extern def infixEq(x: Int, y: Int) at {}: Bool =
  js "${x} === ${y}"
  chez "(equal? ${x} ${y})"
  llvm """
    %z = icmp eq %Int ${x}, ${y}
    %fat_z = zext i1 %z to i64
    %adt_boolean = insertvalue %Pos zeroinitializer, i64 %fat_z, 0
    ret %Pos %adt_boolean
  """
  vm "effekt::infixEq(Int, Int)"

extern def infixNeq(x: Int, y: Int) at {}: Bool =
  js "${x} !== ${y}"
  chez "(not (equal? ${x} ${y}))"
  llvm """
    %z = icmp ne %Int ${x}, ${y}
    %fat_z = zext i1 %z to i64
    %adt_boolean = insertvalue %Pos zeroinitializer, i64 %fat_z, 0
    ret %Pos %adt_boolean
  """
  vm "effekt::infixNeq(Int, Int)"

extern def infixEq(x: Char, y: Char) at {}: Bool =
  js "${x} === ${y}"
  chez "(equal? ${x} ${y})"
  llvm """
    %z = icmp eq %Int ${x}, ${y}
    %fat_z = zext i1 %z to i64
    %adt_boolean = insertvalue %Pos zeroinitializer, i64 %fat_z, 0
    ret %Pos %adt_boolean
  """
  vm "effekt::infixEq(Char, Char)"

extern def infixNeq(x: Char, y: Char) at {}: Bool =
  js "${x} !== ${y}"
  chez "(not (equal? ${x} ${y}))"
  llvm """
    %z = icmp ne %Int ${x}, ${y}
    %fat_z = zext i1 %z to i64
    %adt_boolean = insertvalue %Pos zeroinitializer, i64 %fat_z, 0
    ret %Pos %adt_boolean
  """

extern def infixEq(x: String, y: String) at {}: Bool =
  js "${x} === ${y}"
  chez "(equal? ${x} ${y})"
  llvm """
    %res = call %Pos @c_bytearray_equal(%Pos ${x}, %Pos ${y})
    ret %Pos %res
  """
  vm "effekt::infixEq(String, String)"

def infixNeq(x: String, y: String): Bool = not(x == y)

extern def infixEq(x: Bool, y: Bool) at {}: Bool =
  js "${x} === ${y}"
  chez "(equal? ${x} ${y})"
  llvm """
    %slim_x = extractvalue %Pos ${x}, 0
    %slim_y = extractvalue %Pos ${y}, 0
    %slim_z = icmp eq i64 %slim_x, %slim_y
    %fat_z = zext i1 %slim_z to i64
    %adt_boolean = insertvalue %Pos zeroinitializer, i64 %fat_z, 0
    ret %Pos %adt_boolean
  """
  vm "effekt::infixEq(Bool, Bool)"

extern def infixNeq(x: Bool, y: Bool) at {}: Bool =
  js "${x} !== ${y}"
  chez "(not (equal? ${x} ${y}))"
  llvm """
    %slim_x = extractvalue %Pos ${x}, 0
    %slim_y = extractvalue %Pos ${y}, 0
    %slim_z = icmp ne i64 %slim_x, %slim_y
    %fat_z = zext i1 %slim_z to i64
    %adt_boolean = insertvalue %Pos zeroinitializer, i64 %fat_z, 0
    ret %Pos %adt_boolean
  """


// Math ops
// ========
extern def infixAdd(x: Int, y: Int) at {}: Int =
  js "(${x} + ${y})"
  chez "(+ ${x} ${y})"
  llvm "%z = add  %Int ${x}, ${y} ret %Int %z"
  vm "effekt::infixAdd(Int, Int)"

extern def infixMul(x: Int, y: Int) at {}: Int =
  js "(${x} * ${y})"
  chez "(* ${x} ${y})"
  llvm "%z = mul  %Int ${x}, ${y} ret %Int %z"
  vm "effekt::infixMul(Int, Int)"

extern def infixDiv(x: Int, y: Int) at {}: Int =
  js "Math.floor(${x} / ${y})"
  chez "(floor (/ ${x} ${y}))"
  llvm "%z = sdiv %Int ${x}, ${y} ret %Int %z"
  vm "effekt::infixDiv(Int, Int)"

extern def infixSub(x: Int, y: Int) at {}: Int =
  js "(${x} - ${y})"
  chez "(- ${x} ${y})"
  llvm "%z = sub  %Int ${x}, ${y} ret %Int %z"
  vm "effekt::infixSub(Int, Int)"

extern def mod(x: Int, y: Int) at {}: Int =
  js "(${x} % ${y})"
  chez "(modulo ${x} ${y})"
  llvm "%z = srem %Int ${x}, ${y} ret %Int %z"
  vm "effekt::mod(Int, Int)"

extern def infixAdd(x: Double, y: Double) at {}: Double =
  js "(${x} + ${y})"
  chez "(+ ${x} ${y})"
  llvm "%z = fadd %Double ${x}, ${y} ret %Double %z"
  vm "effekt::infixAdd(Double, Double)"

extern def infixMul(x: Double, y: Double) at {}: Double =
  js "(${x} * ${y})"
  chez "(* ${x} ${y})"
  llvm "%z = fmul %Double ${x}, ${y} ret %Double %z"
  vm "effekt::infixMul(Double, Double)"

extern def infixSub(x: Double, y: Double) at {}: Double =
  js "(${x} - ${y})"
  chez "(- ${x} ${y})"
  llvm "%z = fsub %Double ${x}, ${y} ret %Double %z"
  vm "effekt::infixSub(Double, Double)"

extern def infixDiv(x: Double, y: Double) at {}: Double =
  js "(${x} / ${y})"
  chez "(fl/ ${x} ${y})"
  llvm "%z = fdiv %Double ${x}, ${y} ret %Double %z"
  vm "effekt::infixDiv(Double, Double)"

extern def cos(x: Double) at {}: Double =
  js "Math.cos(${x})"
  chez "(cos ${x})"
  vm "effekt::cos(Double)"
  llvm "%z = call %Double @llvm.cos.f64(double ${x}) ret %Double %z"

extern def sin(x: Double) at {}: Double =
  js "Math.sin(${x})"
  chez "(sin ${x})"
  vm "effekt::sin(Double)"
  llvm "%z = call %Double @llvm.sin.f64(double ${x}) ret %Double %z"

extern def atan(x: Double) at {}: Double =
  js "Math.atan(${x})"
  chez "(atan ${x})"
  vm "effekt::atan(Double)"
  llvm "%z = call %Double @atan(double ${x}) ret %Double %z"

extern def tan(x: Double) at {}: Double =
  js "Math.tan(${x})"
  chez "(tan ${x})"
  vm "effekt::tan(Double)"
  llvm "%z = call %Double @tan(double ${x}) ret %Double %z"

extern def sqrt(x: Double) at {}: Double =
  js "Math.sqrt(${x})"
  chez "(sqrt ${x})"
  llvm "%z = call %Double @llvm.sqrt.f64(double ${x}) ret %Double %z"
  vm "effekt::sqrt(Double)"

def square(x: Double): Double = x * x

def neg(n: Double): Double =
  0.0 - n

def min(n: Double, m: Double): Double =
  if (n < m) n else m

def max(n: Double, m: Double): Double =
  if (n > m) n else m

def abs(n: Double): Double =
  max(n, neg(n))

extern def log(x: Double) at {}: Double =
  js "Math.log(${x})"
  chez "(log ${x})"
  vm "effekt::log(Double)"
  llvm "%z = call %Double @llvm.log.f64(double ${x}) ret %Double %z"

extern def log1p(x: Double) at {}: Double =
  js "Math.log1p(${x})"
  chez "(log (+ ${x} 1))"
  llvm "%z = call %Double @log1p(double ${x}) ret %Double %z"

extern def exp(x: Double) at {}: Double =
  js "Math.exp(${x})"
  chez "(exp ${x})"
  vm "effekt::exp(Double)"
  llvm "%z = call %Double @llvm.exp.f64(double ${x}) ret %Double %z"

def pow(base: Double, exponent: Int): Double = {
  def loop(base: Double, exponent: Int, acc: Double): Double = {
    if (exponent == 0) acc
    else if (exponent.mod(2) == 0) loop(base * base, exponent / 2, acc)  // even exponent
    else loop(base, exponent - 1, acc * base)  // odd exponent
  }
  if (exponent < 0) {
    loop(1.0 / base, 0 - exponent, 1.0)
  } else {
    loop(base, exponent, 1.0)
  }
}

extern def pow(base: Double, exponent: Double) at {}: Double =
  js "Math.pow(${base}, ${exponent})"
  chez "(expt ${base} ${exponent})"
  vm "effekt::pow(Double, Double)"
  llvm "%z = call %Double @llvm.pow.f64(double ${base}, double ${exponent}) ret %Double %z"

// since we do not have "extern val", yet
extern def _pi() at {}: Double =
  js "Math.PI"
  chez "(* 4 (atan 1))"
  vm "effekt::pi()"
  llvm "ret double 3.14159265358979323846264338327950288419716939937510582097494459"

val PI: Double = _pi()

extern def toInt(d: Double) at {}: Int =
  js "Math.trunc(${d})"
  chez "(flonum->fixnum ${d})"
  llvm "%z = fptosi double ${d} to %Int ret %Int %z"
  vm "effekt::toInt(Double)"

extern def toDouble(d: Int) at {}: Double =
  js "${d}"
  chez "(fixnum->flonum ${d})"
  llvm "%z = sitofp i64 ${d} to double ret double %z"
  vm "effekt::toDouble(Int)"


extern def round(d: Double) at {}: Int =
  js "Math.round(${d})"
  chez "(flonum->fixnum (round ${d}))"
  llvm """
    %i = call %Double @llvm.round.f64(double ${d})
    %z = fptosi double %i to %Int ret %Int %z
  """
  vm "effekt::round(Double)"

def round(d: Double, digits: Int): Double = {
  val factor = pow(10.0, digits)
  round(d * factor).toDouble / factor
}

extern def floor(d: Double) at {}: Int =
  js "Math.floor(${d})"
  chez "(flonum->fixnum (floor ${d}))"
  llvm """
    %i = call %Double @llvm.floor.f64(double ${d})
    %z = fptosi double %i to %Int ret %Int %z
  """

extern def ceil(d: Double) at {}: Int =
  js "Math.ceil(${d})"
  chez "(flonum->fixnum (ceiling ${d}))"
  llvm """
    %i = call %Double @llvm.ceil.f64(double ${d})
    %z = fptosi double %i to %Int ret %Int %z
  """

def min(n: Int, m: Int): Int =
  if (n < m) n else m

def max(n: Int, m: Int): Int =
  if (n > m) n else m

def neg(n: Int): Int =
    0 - n

def abs(n: Int): Int =
    max(n, neg(n))


extern def infixLt(x: Int, y: Int) at {}: Bool =
  js "(${x} < ${y})"
  chez "(< ${x} ${y})"
  llvm """
    %z = icmp slt %Int ${x}, ${y}
    %fat_z = zext i1 %z to i64
    %adt_boolean = insertvalue %Pos zeroinitializer, i64 %fat_z, 0
    ret %Pos %adt_boolean
  """
  vm "effekt::infixLt(Int, Int)"

extern def infixLte(x: Int, y: Int) at {}: Bool =
  js "(${x} <= ${y})"
  chez "(<= ${x} ${y})"
  llvm """
    %z = icmp sle %Int ${x}, ${y}
    %fat_z = zext i1 %z to i64
    %adt_boolean = insertvalue %Pos zeroinitializer, i64 %fat_z, 0
    ret %Pos %adt_boolean
  """
  vm "effekt::infixLte(Int, Int)"

extern def infixGt(x: Int, y: Int) at {}: Bool =
  js "(${x} > ${y})"
  chez "(> ${x} ${y})"
  llvm """
    %z = icmp sgt %Int ${x}, ${y}
    %fat_z = zext i1 %z to i64
    %adt_boolean = insertvalue %Pos zeroinitializer, i64 %fat_z, 0
    ret %Pos %adt_boolean
  """
  vm "effekt::infixGt(Int, Int)"

extern def infixGte(x: Int, y: Int) at {}: Bool =
  js "(${x} >= ${y})"
  chez "(>= ${x} ${y})"
  llvm """
    %z = icmp sge %Int ${x}, ${y}
    %fat_z = zext i1 %z to i64
    %adt_boolean = insertvalue %Pos zeroinitializer, i64 %fat_z, 0
    ret %Pos %adt_boolean
  """
  vm "effekt::infixGte(Int, Int)"

extern def infixEq(x: Double, y: Double) at {}: Bool =
  js "${x} === ${y}"
  chez "(= ${x} ${y})"
  vm "effekt::infixEq(Double, Double)"

extern def infixNeq(x: Double, y: Double) at {}: Bool =
  js "${x} !== ${y}"
  chez "(not (= ${x} ${y}))"
  vm "effekt::infixNeq(Double, Double)"

extern def infixLt(x: Double, y: Double) at {}: Bool =
  js "(${x} < ${y})"
  chez "(< ${x} ${y})"
  llvm """
    %z = fcmp olt %Double ${x}, ${y}
    %fat_z = zext i1 %z to i64
    %adt_boolean = insertvalue %Pos zeroinitializer, i64 %fat_z, 0
    ret %Pos %adt_boolean
  """
  vm "effekt::infixLt(Double, Double)"

extern def infixLte(x: Double, y: Double) at {}: Bool =
  js "(${x} <= ${y})"
  chez "(<= ${x} ${y})"
  vm "effekt::infixLte(Double, Double)"

extern def infixGt(x: Double, y: Double) at {}: Bool =
  js "(${x} > ${y})"
  chez "(> ${x} ${y})"
  llvm """
    %z = fcmp ogt %Double ${x}, ${y}
    %fat_z = zext i1 %z to i64
    %adt_boolean = insertvalue %Pos zeroinitializer, i64 %fat_z, 0
    ret %Pos %adt_boolean
  """
  vm "effekt::infixGt(Double, Double)"

extern def infixGte(x: Double, y: Double) at {}: Bool =
  js "(${x} >= ${y})"
  chez "(>= ${x} ${y})"
  vm "effekt::infixGte(Double, Double)"

// TODO do we really need those? if yes, move to string.effekt
extern def infixLt(x: String, y: String) at {}: Bool =
  js "(${x} < ${y})"
  chez "(string<? ${x} ${y})"

extern def infixLte(x: String, y: String) at {}: Bool =
  js "(${x} <= ${y})"
  chez "(string<=? ${x} ${y})"

extern def infixGt(x: String, y: String) at {}: Bool =
  js "(${x} > ${y})"
  chez "(string>? ${x} ${y})"

extern def infixGte(x: String, y: String) at {}: Bool =
  js "(${x} >= ${y})"
  chez "(string>=? ${x} ${y})"


// Boolean operations
// ==================
extern def not(b: Bool) at {}: Bool =
  js "!${b}"
  chez "(not ${b})"
  llvm """
    %p = extractvalue %Pos ${b}, 0
    %q = xor i64 1, %p
    %adt_q = insertvalue %Pos zeroinitializer, i64 %q, 0
    ret %Pos %adt_q
  """
  vm "effekt::not(Bool)"

def infixOr { first: => Bool } { second: => Bool }: Bool =
  if (first()) true else second()

def infixAnd { first: => Bool } { second: => Bool }: Bool =
  if (first()) second() else false


// Bitwise operations
// ==================

/// Arithmetic left shift
extern def bitwiseShl(x: Int, y: Int) at {}: Int =
  js "(${x} << ${y})"
  chez "(ash ${x} ${y})"
  llvm "%z = shl %Int ${x}, ${y} ret %Int %z"
  vm "effekt::bitwiseShl(Int, Int)"

/// Arithmetic right shift
extern def bitwiseShr(x: Int, y: Int) at {}: Int =
  js "(${x} >> ${y})"
  chez "(ash ${x} (- ${y}))"
  llvm "%z = ashr %Int ${x}, ${y} ret %Int %z"
  vm "effekt::bitwiseShr(Int, Int)"

extern def bitwiseAnd(x: Int, y: Int) at {}: Int =
  js "(${x} & ${y})"
  chez "(logand ${x} ${y})"
  llvm "%z = and %Int ${x}, ${y} ret %Int %z"
  vm "effekt::bitwiseAnd(Int, Int)"

extern def bitwiseOr(x: Int, y: Int) at {}: Int =
  js "(${x} | ${y})"
  chez "(logior ${x} ${y})"
  llvm "%z = or %Int ${x}, ${y} ret %Int %z"
  vm "effekt::bitwiseOr(Int, Int)"

extern def bitwiseXor(x: Int, y: Int) at {}: Int =
  js "(${x} ^ ${y})"
  chez "(logxor ${x} ${y})"
  llvm "%z = xor %Int ${x}, ${y} ret %Int %z"
  vm "effekt::bitwiseXor(Int, Int)"


// Byte operations
// ===============
extern def toByte(n: Int) at {}: Byte =
  js "${n}"
  llvm "%z = trunc %Int ${n} to %Byte ret %Byte %z"
  chez "${n}"

extern def toInt(n: Byte) at {}: Int =
  js "${n}"
  llvm "%z = zext %Byte ${n} to %Int ret %Int %z"
  vm "effekt::toInt(Byte)"
  chez "${n}"


// Undefined and Null
// ==================
// Should only be used for interop with FFI, since values in Effekt
// should not be undefined.
//
//   js:   undefined || null
//   chez: #f
//   llvm: null?

/// The value used by the FFI to represent undefined values
extern def undefined[A]() at {}: A =
  js "undefined"
  chez "#f"
  vm "effekt::undefined()"

/// Is an FFI value undefined?
extern def isUndefined[A](value: A) at {}: Bool =
  js "(${value} === undefined || ${value} === null)"
  chez "(eq? ${value} #f)"
  vm "effekt::isUndefined[A](A)"


// Tuples
// ======
record Tuple2[A, B](first: A, second: B)
record Tuple3[A, B, C](first: A, second: B, third: C)
record Tuple4[A, B, C, D](first: A, second: B, third: C, fourth: D)
record Tuple5[A, B, C, D, E](first: A, second: B, third: C, fourth: D, fifth: E)
record Tuple6[A, B, C, D, E, F](first: A, second: B, third: C, fourth: D, fifth: E, sixth: F)

// OneOrBoth
// =========
type OneOrBoth[A, B] {
  This(a: A)
  That(b: B)
  Both(a: A, b: B)
}

// Control Flow
// ============
interface Control {
  def break(): Unit
  def continue(): Unit
}

def loop { f: () => Unit }: Unit = {
  def go(): Unit = { f(); go() }
  go()
}

def loop { f: {Control} => Unit }: Unit = try {
  def go(): Unit = { f {label}; go() }
  go()
} with label: Control {
  def break() = ()
  def continue() = loop { f }
}

/// Calls provided action repeatedly. `start` is inclusive, `end` is not.
def each(start: Int, end: Int) { action: (Int) => Unit } = {
  def loop(i: Int): Unit =
    if (i < end) { action(i); loop(i + 1) }

  loop(start)
}

/// Calls provided action repeatedly with support for breaking. `start` is inclusive, `end` is not.
def each(start: Int, end: Int) { action: (Int) {Control} => Unit } = {
  var i = start;
  loop { {l} =>
    if (i < end) { val el = i; i = i + 1; action(el){l} }
    else { l.break() }
  }
}

def repeat(n: Int) { action: () => Unit } = each(0, n) { n => action() }

def repeat(n: Int) { action: () {Control} => Unit } = each(0, n) { (n) {l} => action() {l} }

// NOTE: This is emitted by the close hole code action: do not remove unless you also adjust the code action
/// Scopes a local computation
def locally[R] { p: => R } : R = p()

// Splices
// =======
//
// The Effekt compiler translates the string interpolation
//
//     "foo ${42} bar ${43}"
//
// to the following stream of `literal`s and `splice[Int]`s:
//
//     do literal("foo "); do splice(42); do literal(" bar "); do splice(43)
//
// The stream is wrapped into a handler function, which defaults to `stringbuffer::s`
//
//    s { ... }
//
// but can be specified by prefixing the string interpolation `custom"..."`.

effect literal(s: String): Unit
effect splice[A](x: A): Unit


