module partial

import effekt
import option


effect fail(): Nothing

def attempt[R] { program: () => R / fail } { fallback: () => R }: R =
  try {
    program()
  } with fail {
    fallback()
  }

def succeeds { program: () => Unit / fail }: Bool =
  try { program(); true } with fail { false }

def optionally[R] { program: () => R / fail }: Option[R] =
  try {
    Some(program())
  } with fail {
    None()
  }

def must[R] { program: () => R / stop }: R / fail =
  try { program() } with stop { resume(do fail()) }

/// Run `program` forever until `fail` is thrown.
def exhaustively { program: () => Unit / fail }: Unit =
  try {
    def go(): Unit = {
      program()
      go()
    }
    go()
  } with fail {
    ()
  }

def crash[R](message: String) { program: () => R / fail }: R =
  attempt { program() } { panic(message) }

def suchThat[A](value: A){ predicate: A => Bool }: Option[A] =
  if (predicate(value)) { Some(value) } else { None() }

def getOrFail[A](option: Option[A]): A / fail =
  option.getOrElse { do fail() }
