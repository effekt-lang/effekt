module ref

extern js """
function set$impl(ref, value) {
  ref.value = value;
  return $effekt.unit;
}
"""

/**
 * Global, mutable references
 */
extern type Ref[T]

extern global def fresh[T](init: T): Ref[T] =
  js "{ value: ${init} }"
  chez "(box ${init})"
  ml "ref ${init}"
  llvm """
    %z = call %Pos @c_ref_fresh(%Pos %init)
    ret %Pos %z
  """

extern global def get[T](ref: Ref[T]): T =
  js "${ref}.value"
  chez "(unbox ${ref})"
  ml "!${ref}"
  llvm """
    %z = call %Pos @c_ref_get(%Pos %ref)
    ret %Pos %z
  """

extern global def set[T](ref: Ref[T], value: T): Unit =
  js "set$impl(${ref}, ${value})"
  chez "(set-box! ${ref} ${value})"
  ml "${ref} := ${value}"
  llvm """
    %z = call %Pos @c_ref_set(%Pos %ref, %Pos %value)
    ret %Pos %z
  """