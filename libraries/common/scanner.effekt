module scanner

import stream

interface Scan[A] {
  /// Return the next character, not advancing.
  /// That is, this does not change what any future calls on the Scanner return.
  def peek(): A / stop
  /// Advance the Scanner to the next character.
  def skip(): Unit / stop
}

/// Advance the Scanner to the next character, returning it.
def read[A](): A / { Scan[A], stop } = {
  val t = do peek()
  do skip()
  return t
}

/// Run a scanner by reading from an input stream, discarding its output.
def scanner[A] { scanner: () => Unit / Scan[A] }: Unit / read[A] =
  returning::scanner[A, Unit]{scanner}

def expect(message: String) { scanner: () => Unit / stop }: Unit / Exception[WrongFormat] =
  returning::expect[Unit](message){scanner}

def probe { scanner: () => Unit / stop }: Bool =
  returning::probe[Unit]{scanner}.isDefined


/// Check that the next token satisfies the predicate and skip and return it when it does.
def readIf[A] { predicate: A => Bool }: A / { Scan[A], stop } = {
  val t = do peek()
  if (predicate(t)) {
    do skip()
    return t
  } else {
    do stop()
  }
}

/// Check that the next token satisfies the predicate and skip it when it does.
def skipIf[A] { predicate: A => Bool }: Unit / { Scan[A], stop } = {
  readIf{predicate}; ()
}

def readSome[A, B] { convert: A => Option[B] }: B / { Scan[A], stop } =
  if (convert(do peek()) is Some(t)) {
    do skip()
    return t
  } else {
    do stop()
  }

/// like `readSome`, but for partial functions `A => B / fail` such as `char::digitValue`
def tryRead[A, B] { convert: A => B / fail }: B / { Scan[A], stop } = {
  val t = attempt { convert(do peek()) } { do stop() }
  do skip()
  return t
}

/// Reads until the predicate does not hold for the next token.
/// Emits the tokens read.
def readWhile[A] { predicate: A => Bool }: Unit / { Scan[A], emit[A] } =
  many { readIf{predicate} }

/// Skips until the predicate does not hold for the next token.
def skipWhile[A] { predicate: A => Bool }: Unit / { Scan[A] } =
  for[A] { readWhile{predicate} } { _ => () }

/// Read the next character if it is the given one.
def readIf(e: Char): Unit / { Scan[Char], stop } = {
  readIf[Char] { c => c == e }; ()
}

/// Check that the next token satisfies the predicate and skip it when it does.
def skipIf(e: Char): Unit / { Scan[Char], stop } =
  skipIf { c => c == e }

/// Read values produced by `action` followed by `separator` until one of them stops.
def separating[A] { seperator: () => Unit / stop } { action: () => A / stop }: Unit / emit[A] =
  exhaustively {
    do emit(action())
    seperator()
  }

/// Read a sequence separated by `separator`.
def separating[A](seperator: Char) { action: () => A / stop }: Unit / { Scan[Char], emit[A] } =
  separating { skipIf(seperator) } {action}


/// Skip until the next character is not a whitespace character
def skipWhitespace(): Unit / { Scan[Char] } =
  skipWhile { c => c.isWhitespace }

/// Read the characters of the given stringing otherwise.
def readString(string: String): Unit / { Scan[Char], stop } =
  for[Char] { string.each } { char => readIf(char) }

/// Check that the next character is a digit in base 10, and if so read and return it.
def readDigit(): Int / { Scan[Char], stop } =
  tryRead[Char, Int]{ char => digitValue(char) }

/// Read a positive decimal number.
def readDecimal(): Int / Scan[Char] = {
  var result = 0
  for[Int] { many { readDigit() } } { digit =>
    result = result * 10 + digit
  }
  result
}

/// Check that the next character is a digit in base 16, and if so read and return it.
def readHexDigit(): Int / { Scan[Char], stop } =
  tryRead[Char, Int]{ char => hexDigitValue(char) }

/// Read a hexadecimal number as long as possible.
def readHexadecimal(): Int / Scan[Char] = {
  var result = 0
  for[Int] { many { readHexDigit() } } { digit =>
    result = result * 16 + digit
  }
  result
}

/// Read a decimal integer as long as possible.
def readInteger(): Int / Scan[Char] =
  if (returning::probe { readIf('-') } is Some(())) {
    neg(readDecimal())
  } else {
    readDecimal()
  }


namespace returning {

/// Run a scanner by reading from an input stream.
def scanner[A, R] { scanner: () => R / Scan[A] }: R / read[A] = {
  var last = None()
  try {
    scanner()
  } with Scan[A] {
    def peek() = resume {
      last match {
        case None() =>
          val t = do read()
          last = Some(t)
          return t
        case Some(t) =>
          return t
      }
    }
    def skip() = resume {
      last match {
        case None() =>
          val _ = do read()
          return ()
        case Some(t) =>
          last = None()
          return ()
      }
    }
  }
}

def expect[R](message: String) { scanner: () => R / stop }: R / Exception[WrongFormat] =
  try { scanner() } with stop { wrongFormat(message) }

def probe[R] { scanner: () => R / stop }: Option[R] =
  try { Some(scanner()) } with stop { None() }

}


namespace test {
  def main(): Unit = {
  
    val input =
      """
      12
      -13
      A1B2
      """

    with feed(input)
    with scanner[Char]

    skipWhitespace()
    println(readInteger().show)
    skipWhitespace()
    println(readInteger().show)
    skipWhitespace()
    println(readHexadecimal().show)
  }
}
