module stream

import array
import bytearray
import io/filesystem
import io/error


effect emit[A](value: A): Unit

def for[A, R] { stream: () => R / emit[A] } { action: A => Unit }: R =
  try {
    stream()
  } with emit[A] {
    def emit(value) = resume(action(value))
  }

def for[A] { stream: () => Unit / emit[A] } { action: A => Unit }: Unit =
  for[A, Unit]{stream}{action}

def continually[A] { action: () => A }: Nothing / emit[A] = {
  def go(): Nothing = {
    do emit(action())
    go()
  }
  go()
}


def each[A](list: List[A]): Unit / emit[A] =
  list match {
    case Nil() => ()
    case Cons(head, tail) =>
      do emit(head)
      each(tail)
  }

def each[A](array: Array[A]): Unit / emit[A] = {
  val n = array.size
  def go(i: Int): Unit = {
    if (i < n) {
      do emit(array.unsafeGet(i))
      go(i + 1)
    }
  }
  go(0)
}

def each(bytes: ByteArray): Unit / emit[Byte] = {
  val n = bytes.size
  def go(i: Int): Unit = {
    if (i < n) {
      do emit(bytes.unsafeGet(i))
      go(i + 1)
    }
  }
  go(0)
}


// not option
// not dequeue
// not queue
// not result
// not seq


effect stop(): Nothing


def boundary[R] { program: () => R / stop }: Option[R] =
  try {
    Some(program())
  } with stop {
    def stop() = None()
  }

def exhaustively[A] { program: () => A / stop } { action: A => Unit }: Unit =
  try {
    def go(): Unit = {
      action(program())
      go()
    }
    go()
  } with stop {
    def stop() = ()
  }

record Indexed[A](index: Int, value: A)

// In Effekt lower bounds are inclusive and upper bounds are exclusive

def range(lower: Int, upper: Int): Unit / emit[Int] =
  if (lower < upper) {
    do emit(lower)
    range(lower + 1, upper)
  }

def rangeTo(upper: Int): Unit / emit[Int] =
  range(0, upper)

def rangeFrom(lower: Int): Unit / emit[Int] = {
  do emit(lower)
  rangeFrom(lower + 1)
}


def index[A, R] { stream: () => R / emit[A] }: R / emit[Indexed[A]] = {
  var i = 0;
  try {
    stream()
  } with emit[A] {
    def emit(v) = {
      val c = i;
      i = c + 1;
      resume(do emit(Indexed(c, v)))
    }
  }
}


def take[A, R](number: Int) { stream: () => R / emit[A] }: R / { emit[A], stop } = {
  var i = 0;
  try {
    stream()
  } with emit[A] {
    def emit(v) = {
      val c = i;
      if (c < number) {
        i = c + 1;
        resume(do emit(v))
      } else {
        do stop()
      }
    }
  }
}


// If `number` is zero or negative it does nothing (like `stream::take`)
def repeat[A](number: Int) { action: () => A }: Unit / emit[A] =
  if (number > 0) {
    do emit(action())
    repeat(number - 1) {action}
  }


def sum[R] { stream : () => R / emit[Int] }: (R, Int) = {
  var s = 0;
  try {
    (stream(), s)
  } with emit[Int] {
    def emit(v) = {
      s = s + v;
      resume(())
    }
  }
}

def sum { stream : () => Unit / emit[Int] }: Int =
  sum[Unit]{stream}.second

def collectList[A, R] { stream: () => R / emit[A] }: (R, List[A]) =
  try {
    (stream(), Nil())
  } with emit[A] {
    def emit(v) = {
      val (r, vs) = resume(());
      (r, Cons(v, vs))
    }
  }

def collectList[A] { stream: () => Unit / emit[A] }: List[A] =
  collectList[A, Unit]{stream}.second

def collectArray[A, R] { stream: () => R / emit[A] }: (R, Array[A]) = {
  var i = 0
  var a = array::allocate(1)
  try {
    (stream(), a.resize(i))
  } with emit[A] {
    def emit(v) = {
      if (i >= a.size) { a = a.resize(2 * a.size) }
      a.unsafeSet(i, v)
      i = i + 1
      resume(())
    }
  }
}

def collectArray[A] { stream: () => Unit / emit[A] }: Array[A] =
  collectArray[A, Unit]{stream}.second

def collectBytes[R] { stream: () => R / emit[Byte] }: (R, ByteArray) = {
  var i = 0
  var a = bytearray::allocate(1)
  try {
    (stream(), a.resize(i))
  } with emit[Byte] {
    def emit(v) = {
      if (i >= a.size) { a = a.resize(2 * a.size) }
      a.unsafeSet(i, v)
      i = i + 1
      resume(())
    }
  }
}

def collectBytes { stream: () => Unit / emit[Byte] }: ByteArray =
  collectBytes[Unit]{stream}.second



effect read[A](): A / stop


def source[A, R] { stream: () => Unit / emit[A] } { reader: () => R / read[A] }: R = {
  var next = box { () => None() }
  next = box { () =>
    try {
      stream()
      None()
    } with emit[A] {
      def emit(v) = {
        next = box { () => resume(()) }
        Some(v)
      }
    }
  }
  try {
    reader()
  } with read[A] {
    def read() = resume {
      next().getOrElse { do stop() }
    }
  }
}


def writeFile[R](file: File) { stream: () => R / emit[Byte] }: R / Exception[IOError] = {

  val chunkSize = 1048576 // 1MB
  val buffer = bytearray::allocate(chunkSize)
  var offset = 0

  def push(i: Int, n: Int): Unit = {
    val r = write(file, buffer, i, n, -1)
    if (r < n) {
      push(i + r, n - r)
    }
  }

  try {
    val r = stream()
    push(0, offset)
    return r
  } with emit[Byte] {
    def emit(byte) = {
      if (offset >= buffer.size) { push(0, buffer.size) }
      buffer.unsafeSet(offset, byte)
      offset = offset + 1
      resume(())
    }
  }
}

def readFile[R](file: File) { reader: () => R / read[Byte] }: R / Exception[IOError] = {

  val chunkSize = 1048576 // 1MB
  val buffer = bytearray::allocate(chunkSize)
  var offset = 0
  var length = 0

  def pull(): Unit / stop = {
    read(file, buffer, 0, chunkSize, -1) match {
      case 0 =>
        do stop()
      case n =>
        offset = 0
        length = n
    }
  }

  try {
    reader()
  } with read[Byte] {
    def read() = resume {
      if (offset >= length) { pull() }
      val byte = buffer.unsafeGet(offset)
      offset = offset + 1
      return byte
    }
  }
}

type DecoderState {
  Initial()
  Expect1(acc: Int)
  Expect2(acc: Int)
  Expect3(acc: Int)
}

def decodeChar(state: DecoderState, byte: Byte): DecoderState / emit[Char] = {
  val b = byte.toInt
  state match {
    case Initial() =>
      if (b < 128) {
        do emit(b.toChar)
        Initial()
      } else if (b < 224) {
        Expect1(bitwiseAnd(b, 31))
      } else if (b < 240) {
        Expect2(bitwiseAnd(b, 15))
      } else {
        Expect3(bitwiseAnd(b, 7))
      }
    case Expect1(acc) =>
      do emit(bitwiseOr(bitwiseShl(acc, 6), bitwiseAnd(b, 63)).toChar)
      Initial()
    case Expect2(acc) =>
      Expect1(bitwiseOr(bitwiseShl(acc, 6), bitwiseAnd(b, 63)))
    case Expect3(acc) =>
      Expect2(bitwiseOr(bitwiseShl(acc, 6), bitwiseAnd(b, 63)))
  }
}

def encodeChar(char: Char): Unit / emit[Byte] = {
  val code = char.toInt
  if (code < 128) {
    do emit(code.toByte)
  } else if (code < 2048) {
    do emit(bitwiseOr(192, bitwiseShr(code, 6)).toByte)
    do emit(bitwiseOr(128, bitwiseAnd(code, 63)).toByte)
  } else if (code < 65536) {
    do emit(bitwiseOr(224, bitwiseShr(code, 12)).toByte)
    do emit(bitwiseOr(128, bitwiseAnd(bitwiseShr(code, 6), 63)).toByte)
    do emit(bitwiseOr(128, bitwiseAnd(code, 63)).toByte)
  } else {
    do emit(bitwiseOr(240, bitwiseShr(code, 18)).toByte)
    do emit(bitwiseOr(128, bitwiseAnd(bitwiseShr(code, 12), 63)).toByte)
    do emit(bitwiseOr(128, bitwiseAnd(bitwiseShr(code, 6), 63)).toByte)
    do emit(bitwiseOr(128, bitwiseAnd(code, 63)).toByte)
  }
}

def decodeUTF8[R] { stream: () => R / emit[Byte] }: R / emit[Char] = {
  var state = Initial()
  try {
    stream()
  } with emit[Byte] {
    def emit(byte) = resume(state = decodeChar(state, byte))
  }
}

def encodeUTF8[R] { stream: () => R / emit[Char] }: R / emit[Byte] =
  try {
    stream()
  } with emit[Char] {
    def emit(char) = resume(encodeChar(char))
  }

def each(string: String): Unit / emit[Char] =
  decodeUTF8 { string.fromString.each }

def collectString[R] { stream: () => R / emit[Char] }: (R, String) = {
  val (result, bytes) = collectBytes { encodeUTF8 { stream } }
  (result, bytes.intoString)
}

def collectString { stream: () => Unit / emit[Char] }: String =
  collectString[Unit]{stream}.second




