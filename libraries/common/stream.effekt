module stream

import array
import bytearray
import io/filesystem
import io/error


interface Emit[A] {
  def emit(value: A): Unit
}

def for[A, R] { stream: () => R / Emit[A] } { action: A => Unit }: R =
  try {
    stream()
  } with Emit[A] {
    def emit(value) = resume(action(value))
  }

def for[A] { stream: () => Unit / Emit[A] } { action: A => Unit }: Unit =
  for[A, Unit]{stream}{action}

def forever[A] { action: () => A }: Nothing / Emit[A] = {
  def go(): Nothing = {
    do emit(action())
    go()
  }
  go()
}


def each[A](list: List[A]): Unit / Emit[A] =
  list match {
    case Nil() => ()
    case Cons(head, tail) =>
      do emit(head)
      each(tail)
  }

def each[A](array: Array[A]): Unit / Emit[A] = {
  val n = array.size
  def go(i: Int): Unit = {
    if (i < n) {
      do emit(array.unsafeGet(i))
      go(i + 1)
    }
  }
  go(0)
}

def each(bytes: ByteArray): Unit / Emit[Byte] = {
  val n = bytes.size
  def go(i: Int): Unit = {
    if (i < n) {
      do emit(bytes.unsafeGet(i))
      go(i + 1)
    }
  }
  go(0)
}

def writeFile[R](file: File) { stream: () => R / Emit[Byte] }: R / Exception[IOError] = {

  val chunkSize = 1048576 // 1MB
  val buffer = bytearray::allocate(chunkSize)
  var offset = 0

  def push(i: Int, n: Int): Unit = {
    val r = write(file, buffer, i, n, -1)
    if (r < n) {
      push(i + r, n - r)
    }
  }

  try {
    val r = stream()
    push(0, offset)
    return r
  } with Emit[Byte] {
    def emit(byte) = {
      if (offset >= buffer.size) { push(0, buffer.size) }
      buffer.unsafeSet(offset, byte)
      offset = offset + 1
      resume(())
    }
  }
}

// not option
// not dequeue
// not queue
// not result
// not seq


interface Done {
  def done(): Nothing
}

def boundary[R] { program: () => R / Done }: Option[R] =
  try {
    Some(program())
  } with Done {
    def done() = None()
  }


record Indexed[A](index: Int, value: A)

// In Effekt lower bounds are inclusive and upper bounds are exclusive

def range(lower: Int, upper: Int): Unit / Emit[Int] =
  if (lower < upper) {
    do emit(lower)
    range(lower + 1, upper)
  }

def rangeTo(upper: Int): Unit / Emit[Int] =
  range(0, upper)

def rangeFrom(lower: Int): Unit / Emit[Int] = {
  do emit(lower)
  rangeFrom(lower + 1)
}


def index[A, R] { stream: () => R / Emit[A] }: R / Emit[Indexed[A]] = {
  var i = 0;
  try {
    stream()
  } with Emit[A] {
    def emit(v) = {
      val c = i;
      i = c + 1;
      resume(do emit(Indexed(c, v)))
    }
  }
}


def take[A, R](number: Int) { stream: () => R / Emit[A] }: R / { Emit[A], Done } = {
  var i = 0;
  try {
    stream()
  } with Emit[A] {
    def emit(v) = {
      val c = i;
      if (c < number) {
        i = c + 1;
        resume(do emit(v))
      } else {
        do done()
      }
    }
  }
}


// If `number` is zero or negative it does nothing (like `stream::take`)
def repeat[A](number: Int) { action: () => A }: Unit / Emit[A] =
  if (number > 0) {
    do emit(action())
    repeat(number - 1) {action}
  }


def sum[R] { stream : () => R / Emit[Int] }: (R, Int) = {
  var s = 0;
  try {
    (stream(), s)
  } with Emit[Int] {
    def emit(v) = {
      s = s + v;
      resume(())
    }
  }
}

def sum { stream : () => Unit / Emit[Int] }: Int =
  sum[Unit]{stream}.second

def gatherList[A, R] { stream: () => R / Emit[A] }: (R, List[A]) =
  try {
    (stream(), Nil())
  } with Emit[A] {
    def emit(v) = {
      val (r, vs) = resume(());
      (r, Cons(v, vs))
    }
  }

def gatherList[A] { stream: () => Unit / Emit[A] }: List[A] =
  gatherList[A, Unit]{stream}.second

def gatherArray[A, R] { stream: () => R / Emit[A] }: (R, Array[A]) = {
  var i = 0
  var a = array::allocate(1)
  try {
    (stream(), a.resize(i))
  } with Emit[A] {
    def emit(v) = {
      if (i >= a.size) { a = a.resize(2 * a.size) }
      a.unsafeSet(i, v)
      i = i + 1
      resume(())
    }
  }
}

def gatherArray[A] { stream: () => Unit / Emit[A] }: Array[A] =
  gatherArray[A, Unit]{stream}.second

def gatherBytes[R] { stream: () => R / Emit[Byte] }: (R, ByteArray) = {
  var i = 0
  var a = bytearray::allocate(1)
  try {
    (stream(), a.resize(i))
  } with Emit[Byte] {
    def emit(v) = {
      if (i >= a.size) { a = a.resize(2 * a.size) }
      a.unsafeSet(i, v)
      i = i + 1
      resume(())
    }
  }
}

def gatherBytes { stream: () => Unit / Emit[Byte] }: ByteArray =
  gatherBytes[Unit]{stream}.second



interface Read[A] {
  def read(): A / Done
}

def source[A, R] { stream: () => Unit / Emit[A] } { reader: () => R / Read[A] }: R = {
  var next = box { () => None() }
  next = box { () =>
    try {
      stream()
      None()
    } with Emit[A] {
      def emit(v) = {
        next = box { () => resume(()) }
        Some(v)
      }
    }
  }
  try {
    reader()
  } with Read[A] {
    def read() = resume {
      next().getOrElse { do done() }
    }
  }
}

def readFile[R](file: File) { reader: () => R / Read[Byte] }: R / Exception[IOError] = {

  val chunkSize = 1048576 // 1MB
  val buffer = bytearray::allocate(chunkSize)
  var offset = 0
  var length = 0

  def pull(): Unit / Done = {
    read(file, buffer, 0, chunkSize, -1) match {
      case 0 =>
        do done()
      case n =>
        offset = 0
        length = n
    }
  }

  try {
    reader()
  } with Read[Byte] {
    def read() = resume {
      if (offset >= length) { pull() }
      val byte = buffer.unsafeGet(offset)
      offset = offset + 1
      return byte
    }
  }
}

type DecoderState {
  Initial()
  Expect1(acc: Int)
  Expect2(acc: Int)
  Expect3(acc: Int)
}

def decodeChar(state: DecoderState, byte: Byte): DecoderState / Emit[Char] = {
  val b = byte.toInt
  state match {
    case Initial() =>
      if (b < 128) {
        do emit(b.toChar)
        Initial()
      } else if (b < 224) {
        Expect1(bitwiseAnd(b, 31))
      } else if (b < 240) {
        Expect2(bitwiseAnd(b, 15))
      } else {
        Expect3(bitwiseAnd(b, 7))
      }
    case Expect1(acc) =>
      do emit(bitwiseOr(bitwiseShl(acc, 6), bitwiseAnd(b, 63)).toChar)
      Initial()
    case Expect2(acc) =>
      Expect1(bitwiseOr(bitwiseShl(acc, 6), bitwiseAnd(b, 63)))
    case Expect3(acc) =>
      Expect2(bitwiseOr(bitwiseShl(acc, 6), bitwiseAnd(b, 63)))
  }
}

def encodeChar(char: Char): Unit / Emit[Byte] = {
  val code = char.toInt
  if (code < 128) {
    do emit(code.toByte)
  } else if (code < 2048) {
    do emit(bitwiseOr(192, bitwiseShr(code, 6)).toByte)
    do emit(bitwiseOr(128, bitwiseAnd(code, 63)).toByte)
  } else if (code < 65536) {
    do emit(bitwiseOr(224, bitwiseShr(code, 12)).toByte)
    do emit(bitwiseOr(128, bitwiseAnd(bitwiseShr(code, 6), 63)).toByte)
    do emit(bitwiseOr(128, bitwiseAnd(code, 63)).toByte)
  } else {
    do emit(bitwiseOr(240, bitwiseShr(code, 18)).toByte)
    do emit(bitwiseOr(128, bitwiseAnd(bitwiseShr(code, 12), 63)).toByte)
    do emit(bitwiseOr(128, bitwiseAnd(bitwiseShr(code, 6), 63)).toByte)
    do emit(bitwiseOr(128, bitwiseAnd(code, 63)).toByte)
  }
}

def decodeUTF8[R] { stream: () => R / Emit[Byte] }: R / Emit[Char] = {
  var state = Initial()
  try {
    stream()
  } with Emit[Byte] {
    def emit(byte) = resume(state = decodeChar(state, byte))
  }
}

def encodeUTF8[R] { stream: () => R / Emit[Char] }: R / Emit[Byte] =
  try {
    stream()
  } with Emit[Char] {
    def emit(char) = resume(encodeChar(char))
  }

def each(string: String): Unit / Emit[Char] =
  decodeUTF8 { string.fromString.each }

def gatherString[R] { stream: () => R / Emit[Char] }: (R, String) = {
  val (result, bytes) = gatherBytes { encodeUTF8 { stream } }
  (result, bytes.intoString)
}

def gatherString { stream: () => Unit / Emit[Char] }: String =
  gatherString[Unit]{stream}.second




