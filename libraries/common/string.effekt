module string

import effekt
import option
import stream
import list
import exception
import result
import char
import bytearray

// TODO
// - [ ] handle unicode codepoints (that can span two indices) correctly
// - [ ] use string buffers or other buffers to implement repeated concatenation more efficiently (and `join`)
// - [ ] maybe use native implementations for toInt, repeat, split, indexOf, etc.

/**
 * Strings
 * - JS: Strings are represented as UTF-16 code units where some characters take 2 slots (surrogate pairs)
 * - Chez: Strings are sequences of unicode characters (?)
 * - LLVM: UTF-8 (characters can take from 1-4 bytes).
 */


// Operations
// ----------

def substring(str: String, from: Int, to: Int): String = {
  def clamp(lower: Int, x: Int, upper: Int) = max(lower, min(x, upper))
  val lower = clamp(0, from, to)
  val upper = clamp(0, to, str.length)
  str.unsafeSubstring(lower, upper)
}

def substring(str: String, from: Int): String =
  str.substring(from, str.length)


/// Checks whether str starts with the given prefix at `from`
def isSubstringAt(str: String, prefix: String, from: Int): Bool = {
  with default[OutOfBounds, Bool] { false };

  val stringLength = str.length
  val prefixLength = prefix.length

  def done?(index: Int): Bool = index >= prefixLength
  def matches?(index: Int): Bool = prefix.charAt(index) == str.charAt(index + from)

  def go(index: Int): Bool =
    if (index.done?) true
    else if (index.matches?) go(index + 1) else false

  go(0)
}

def startsWith(str: String, prefix: String): Bool =
  isSubstringAt(str, prefix, 0)

def endsWith(str: String, suffix: String): Bool =
  isSubstringAt(str, suffix, str.length - suffix.length)

/// Repeats the given string n-times.
///
/// TODO use a more efficient way of appending strings like a buffer
def repeat(str: String, n: Int): String = {
  def go(n: Int, result: String): String = {
    if (n <= 0) result
    else go(n - 1, result ++ str)
  }
  go(n, "")
}

/// Left-pad the given string with spaces
def padLeft(str: String, n: Int): String = {
  " ".repeat(n - str.length) ++ str
}

/// Right-pad the given string with spaces
def padRight(str: String, n: Int): String = {
  str ++ " ".repeat(n - str.length)
}

// TODO use .split() in JS
def split(str: String, sep: String): List[String] = {
  val strLength = str.length
  val sepLength = sep.length

  // special case: seperator is empty, just emit individual chars
  def empty(index: Int, components: List[String]): List[String] =
    if (index >= strLength) components.reverse
    else empty(index + 1, Cons(str.substring(index, index + 1), components))

  def go(lastIndex: Int, components: List[String]): List[String] =
    indexOf(str, sep, lastIndex) match {
      case None() =>
        Cons(str.substring(lastIndex, strLength), components).reverse
      case Some(index) =>
        go(index + sepLength, Cons(str.substring(lastIndex, index), components))
    }

  if (sepLength == 0) empty(0, Nil())
  else go(0, Nil())
}

def join(strings: List[String], sep: String): String = {
  def go(strings: List[String], result: String): String = strings match {
    case Nil() => result
    case Cons(s, rest) => go(rest, result ++ sep ++ s)
  }
  strings match {
    case Nil() => ""
    case Cons(first, rest) => go(rest, first)
  }
}

extern def trim(str: String) at {}: String =
  js "${str}.trim()"

// Parsing
// -------
def toBool(s: String): Bool / Exception[WrongFormat] = s match {
  case "true"  => true
  case "false" => false
  case _ => wrongFormat("Not a boolean value: '" ++ s ++ "'")
}

// TODO optimize (right now this will be horribly slow (compared to the native JS version)
def toInt(str: String): Int / Exception[WrongFormat] = {
  def go(index: Int, acc: Int): Int = {
    // wrong index means we're done parsing
    with on[OutOfBounds].default { () => acc };

    val c = str.charAt(index)
    val d = char::digitValue(c)
    go(index + 1, 10 * acc + d)
  }

  with on[OutOfBounds].default {
    wrongFormat("Empty string is not a valid number")
  }

  str.charAt(0) match {
    case '-' => 0 - go(1, 0)
    case _   => go(0, 0)
  }
}

// TODO optimize (right now this will be horribly slow (compared to the native JS version)
def toInt(str: String, base: Int): Int / Exception[WrongFormat] = {
  if (base > 36 || base < 1) {
    wrongFormat("Invalid base: " ++ base.show)
  }

  def go(index: Int, acc: Int): Int = {
    // wrong index means we're done parsing
    with on[OutOfBounds].default { () => acc };

    val c = str.charAt(index)
    val d = char::digitValue(c, base)
    go(index + 1, base * acc + d)
  }

  with on[OutOfBounds].default {
    wrongFormat("Empty string is not a valid number")
  }

  str.charAt(0) match {
    case '-' => 0 - go(1, 0)
    case _   => go(0, 0)
  }
}

/// Native versions of toInt (unused right now)
extern def unsafeToInt(str: String) at {}: Int =
  js "(Number.isNaN(parseInt(${str})) ? undefined : parseInt(${str}))"
  chez "(string->number ${str})"


/// Returns the index of the first occurrence of `sub` in `str`
def indexOf(str: String, sub: String): Option[Int] =
  indexOf(str, sub, 0)

def indexOf(str: String, sub: String, from: Int): Option[Int] = {
  val len = str.length
  def go(index: Int): Option[Int] =
    if (index >= len) None()
    else if (str.isSubstringAt(sub, index)) Some(index)
    else go(index + 1)

  go(from)
}

/// Returns the index of the last occurence of `sub` in `str`
def lastIndexOf(str: String, sub: String): Option[Int] =
  lastIndexOf(str, sub, str.length)

def lastIndexOf(str: String, sub: String, from: Int): Option[Int] = {
  def go(index: Int): Option[Int] =
    if (index < 0) None()
    else if (str.isSubstringAt(sub, index)) Some(index)
    else go(index - 1)

  go(from)
}

// TODO more efficient (native) implementations could be integrated as follows

// extern def indexOf(str: String, sub: String) at {}: Option[Int] =
//   //js { nativeIndexOf(str, sub) }
//   default { indexOf(str, sub, 0) }

// def nativeIndexOf(str: String, sub: String): Option[Int] = {
//   val index = str.unsafeIndexOf(sub)
//   if (index < 0) { None() } else { Some(index) }
// }

// extern def unsafeIndexOf(str: String, sub: String) at {}: Int =
//   js "${str}.indexOf(${sub})"

def charAt(str: String, index: Int): Char / Exception[OutOfBounds] =
  if (index < 0 || index >= length(str))
    do raise(OutOfBounds(), "Index out of bounds: " ++ show(index) ++ " in string: '" ++ str ++ "'")
  else
    unsafeCharAt(str, index)

extern def unsafeCharAt(str: String, n: Int) at {}: Char =
  js "${str}.codePointAt(${n})"
  chez "(char->integer (string-ref ${str} ${n}))"
  llvm """
    %x = call %Int @c_bytearray_character_at(%Pos ${str}, %Int ${n})
    ret %Int %x
  """
  vm "string::unsafeCharAt(String, Int)"


// Streaming
// ---------

def feed[R](string: String) { reader: () => R / read[Char] } =
  feed(string.fromString) {
    decodeUTF8 {
      reader()
    }
  }

def each(string: String): Unit / emit[Char] =
  feed(string) {
    exhaustively {
      do emit(do read())
    }
  }

def collect { stream: () => Unit / emit[Char] }: String =
  returning::collect[Unit]{stream}.second

namespace returning {
  def collect[R] { stream: () => R / emit[Char] }: (R, String) = {
    val (result, bytes) = bytearray::returning::collect[R] { encodeUTF8 { stream } }
    (result, bytes.toString)
  }
}
