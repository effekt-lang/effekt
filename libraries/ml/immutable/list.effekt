module immutable/list

import effekt
import immutable/option

record EmptyList()

type List[A] {
  Nil();
  Cons(head: A, tail: List[A])
}

// def foreach[A](l: List[A]) { f: A => Unit } : Unit = {
//   var remainder = l
//   while(not(remainder.isEmpty)) {
//     remainder match {
//       case Nil() => ()
//       case Cons(a, as) => f(a); remainder = as
//     }
//   }
// }
def foreach[A](l: List[A]) { f: A => Unit } : Unit = l match {
    case Nil() => ()
    case Cons(a, rest) => f(a); foreach(rest){f}
}

// def map[A, B](l: List[A]) { f: A => B } : List[B] = {
//   var acc = Nil[B]()
//   l.foreach { el => acc = Cons(f(el), acc) }
//   acc.reverse
// }
def map[A, B](l: List[A]) { f: A => B }: List[B] = {
    def aux(l: List[A], acc: List[B]): List[B] = l match {
        case Nil() => acc
        case Cons(a, rest) => aux(rest, Cons(f(a), acc))
    };
    aux(l, Nil()).reverse
}

// def size[A](l: List[A]): Int = {
//   var n = 0;
//   l.foreach { _ => n = n + 1 };
//   n
// }
def size[A](l: List[A]): Int = {
    def aux(l: List[A], acc: Int): Int = l match {
        case Nil() => acc
        case Cons(_, rest) => aux(rest, acc+1)
    };
    aux(l, 0)
}

// def reverse[A](l: List[A]): List[A] = {
//   var res = Nil[A]()
//   l.foreach { el => res = Cons(el, res) }
//   res
// }
def reverse[A](l: List[A]): List[A] = {
  def aux(l: List[A], acc: List[A]): List[A] = l match {
    case Nil() => acc
    case Cons(a, rest) => aux(rest, Cons(a, acc))
  };
  aux(l, Nil())
}

// [1,2,3].reverseOnto([4,5,6])
// [3,2,1,4,5,6]
def reverseOnto[A](l: List[A], other: List[A]): List[A] = l match {
  case Nil() => other
  case Cons(a, rest) => rest.reverseOnto(Cons(a, other))
}

// [1,2,3].append([4,5,6])
// [3,2,1].reverseOnto([4,5,6])
// [1,2,3,4,5,6]
def append[A](l: List[A], other: List[A]): List[A] =
  l.reverse.reverseOnto(other)

// gracefully fails
def take[A](l: List[A], n: Int): List[A] =
  if (n == 0) {
    Nil()
  } else l match {
    case Nil() => Nil()
    case Cons(a, rest) => Cons(a, rest.take(n - 1))
  }

def drop[A](l: List[A], n: Int): List[A] =
  if (n == 0) {
    l
  } else l match {
    case Nil() => Nil()
    case Cons(a, rest) => rest.drop(n - 1)
  }

def isEmpty[A](l: List[A]): Boolean = l match {
  case Nil() => true
  case Cons(a, rest) => false
}

def nonEmpty[A](l: List[A]): Boolean = l match {
  case Nil() => false
  case Cons(a, rest) => true
}

// def head[A](l: List[A]): A / Exception[EmptyList] = l match {
//   case Nil() => do raise(EmptyList(), "Trying to get the head of an empty list")
//   case Cons(a, rest) => a
// }

// def tail[A](l: List[A]): List[A] / Exception[EmptyList] = l match {
//   case Nil() => do raise(EmptyList(), "Trying to get the head of an empty list")
//   case Cons(a, rest) => rest
// }

def headOption[A](l: List[A]): Option[A] = l match {
  case Nil() => None()
  case Cons(a, rest) => Some(a)
}

// // elements that satisfy the predicate go into the left list
// def partition[A](l: List[A]) { pred: A => Boolean }: (List[A], List[A]) = {
//   var lefts: List[A] = Nil()
//   var rights: List[A] = Nil()
//   l.foreach { el =>
//     if (pred(el)) { lefts = Cons(el, lefts) }
//     else { rights = Cons(el, rights) }
//   };

//   (lefts.reverse, rights.reverse)
// }
def partition[A](l: List[A]) { pred: A => Boolean }: (List[A], List[A]) = {
    def aux(l: List[A], left: List[A], right: List[A]): (List[A], List[A]) = l match {
        case Nil() => (left, right)
        case Cons(a, rest) =>
            if (pred(a)) aux(rest, Cons(a, left), right)
            else aux(rest, left, Cons(a, right))
    };
    val (leftRev, rightRev) = aux(l, Nil(), Nil());
    (leftRev.reverse, rightRev.reverse)
}

// this implementation is not stacksafe!
def sortBy[A](l: List[A]) { compare: (A, A) => Boolean }: List[A] =
  l match {
    case Nil() => Nil()
    case Cons(pivot, rest) =>
      val (lt, gt) = rest.partition { el => compare(el, pivot) };
      val leftSorted = sortBy(lt) { (a, b) => compare(a, b) }
      val rightSorted = sortBy(gt) { (a, b) => compare(a, b) }
      leftSorted.append(Cons(pivot, rightSorted))
  }

def sort(l: List[Int]): List[Int] = l.sortBy { (a, b) => a < b }
def sort(l: List[Double]): List[Double] = l.sortBy { (a, b) => a < b }
